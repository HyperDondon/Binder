plugins {
    id 'java'
    id 'java-library'
    id "io.github.goooler.shadow" version "8.1.8"
}


group = 'com.hyperdondon.binder'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = uri('https://jitpack.io')
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'org.yaml:snakeyaml:2.3'

    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.hyperdondon.binder.Main'
        )
    }
}
def targetJavaVersion = 8
java {
    withJavadocJar()
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    relocate 'org.yaml.snakeyaml', 'com.hyperdondon.binder.libs.snakeyaml'

    dependencies {
        include(dependency('org.yaml:snakeyaml:2.3'))
    }
}

tasks.build {
    dependsOn tasks.shadowJar
}